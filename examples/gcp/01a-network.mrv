#lang marv

import types/gcp/base
import types/gcp/compute as net

import types/marv/base
import types/marv/enhancers

type label-resource = {
    identity(cfg) = cfg <- { labels = cfg.labels <- { project = cfg.project, cost-ctr = "aabc"  }}
    * = lifecycle.*
}

defaults = {
    project = env("MARV_GCP_PROJECT") 
    region = env("MARV_GCP_REGION") 
    labels = {}
}

type network = E2<net:network, default-resource, label-resource>
type subnet = E2<net:subnetwork, default-resource, label-resource>

module main {

    name = "my-network"
    primary-cidr = "10.0.4.0/24"
    vpc = network {
        name = imm: name
        description = strf("VPC for ~a" name)
        autoCreateSubnetworks = false
        routingConfig = { routingMode = "REGIONAL" }
    } 

    vpc-defaults = defaults <- {
        network = imm: vpc.selfLink
    }

    primary-sn = subnet vpc-defaults <- {
        name = strf("~a-primary" name)
        description = strf("Primary subnet for ~a" name)
        ipCidrRange =  imm: primary-cidr
    }

}